using ModelingToolkit
using ModelingToolkitInputs
using ModelingToolkit: t_nounits as t, D_nounits as D
using OrdinaryDiffEq
using Test

# -----------------------------------------
# ----- example ---------------------------
# -----------------------------------------

vars = @variables begin
    x(t), [input=true]

    # states
    y(t) = 0
end

eqs = [
    D(y) ~ x
]

@mtkcompile sys = InputSystem(eqs, t, vars, []) inputs=[x]



@test !isnothing(ModelingToolkitInputs.get_input_functions(sys))

prob = ODEProblem(sys, [], (0, 4));

# indeterminate form -----------------------

integrator = init(prob, Tsit5())

set_input!(integrator, sys.x, 1.0)
step!(integrator, 1.0, true)

set_input!(integrator, sys.x, 2.0)
step!(integrator, 1.0, true)

set_input!(integrator, sys.x, 3.0)
step!(integrator, 1.0, true)

set_input!(integrator, sys.x, 4.0)
step!(integrator, 1.0, true)

finalize!(integrator)

@test integrator.sol(0.0; idxs = sys.x) == 1.0
@test integrator.sol(1.0; idxs = sys.x) == 2.0
@test integrator.sol(2.0; idxs = sys.x) == 3.0
@test integrator.sol(3.0; idxs = sys.x) == 4.0
@test integrator.sol(4.0; idxs = sys.y) ≈ 10.0

reinit!(integrator)
@test integrator.t == 0

# determinate form -----------------------
input = Input(sys.x, [1, 2, 3, 4], [0, 1, 2, 3])
sol = solve(prob, Tsit5(); inputs=[input]);

@test sol(0.0; idxs = sys.x) == 1.0
@test sol(1.0; idxs = sys.x) == 2.0
@test sol(2.0; idxs = sys.x) == 3.0
@test sol(3.0; idxs = sys.x) == 4.0
@test sol(4.0; idxs = sys.y) ≈ 10.0


# -------------------------------
# bug: multiple variable
vars = @variables begin
    x1(t), [input=true]
    x2(t), [input=true]

    # states
    y(t) = 0
end

eqs = [
    D(y) ~ x1 + x2
]

@mtkcompile sys = InputSystem(eqs, t, vars, []) inputs=[x1, x2]

@test !isnothing(ModelingToolkitInputs.get_input_functions(sys))


# ----------------------------
# issue: 4 (requires MTK v11)
if pkgversion(ModelingToolkit) > v"11"
    @variables x(t)[1:5] [input=true]
    @variables y(t) = 0
    eqs = [D(y) ~ sum(x)]
    @mtkcompile sys=InputSystem(eqs, t, [x, y], []) inputs=[x]
    prob = ODEProblem(sys, [], (0, 4))

    times = collect(0:1:4)
    values = [ones(5)*i for i=1:5]
    x_input = Input(x, values, times)
    sol = solve(prob, Tsit5(); inputs=[x_input])

    @test sol[x[1]] == [1.0, 2.0, 3.0, 4.0, 5.0, 5.0]
end

